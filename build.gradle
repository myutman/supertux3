

buildscript {
    ext.kotlin_version = '1.3.+'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.+'
    }
}


apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

apply plugin: 'application'


group 'ru.hse.supertux3'
version '1.0-SNAPSHOT'

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'build/generated/source/proto/main/java',
                    'build/generated/source/proto/main/grpc'
        }
        kotlin {
            srcDirs 'src/main/kotlin'
        }
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.+'
    compile 'io.grpc:grpc-stub:1.+'
    compile 'io.grpc:grpc-protobuf:1.+'
    compile "io.grpc:grpc-netty:1.+"
    compile "io.grpc:grpc-testing:1.+"
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    compile group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: '2.0.+'
    compile "com.xenomachina:kotlin-argparser:2.0.+"


    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile group: 'org.jline', name: 'jline', version: '3.10.0'
    compile 'com.github.ajalt:mordant:1.2.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile "org.jetbrains.kotlin:kotlin-reflect:1.3.0"

}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.+'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.+'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'ru.hse.supertux3.ui.CLIKt'
        )
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

startScripts.enabled = false

task superTux3Server(type: CreateStartScripts) {
    mainClassName = 'ru.hse.supertux3.multiplayer.SuperTux3ServerKt'
    applicationName = 'super-tux3-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

task superTux3Client(type: CreateStartScripts) {
    mainClassName = 'ru.hse.supertux3.ui.CLIKt'
    applicationName = 'super-tux3-client'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into('bin') {
    from(superTux3Client)
    from(superTux3Server)
    fileMode = 0755
}